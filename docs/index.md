---
page_title: "Provider: Camunda"
description: |-
  The Camunda provider allows to configure resources on the Camunda SaaS platform.
---

# Camunda SaaS Provider

The Camunda providers providers resources to configure clusters, clients, etc.
on the [Camunda SaaS](https://camunda.com/platform/) platform.

## Example Usage

```terraform
# Get the client ID and client secret from https://console.cloud.camunda.io/
variable "camunda_client_id" {}
variable "camunda_client_secret" {}

provider "camunda" {
  client_id     = var.camunda_client_id
  client_secret = var.camunda_client_secret
}

# The channel containing the most recent version of Zeebe.
data "camunda_channel" "alpha" {
  name = "Alpha"
}

# A cluster plan type for default trials.
data "camunda_cluster_plan_type" "trial" {
  name = "Trial Package"
}

# The region associated with the trial plan.
data "camunda_region" "trial" {
  name = data.camunda_cluster_plan_type.trial.region_name
}

resource "camunda_cluster" "test" {
  name = "test"

  channel    = data.camunda_channel.alpha.id
  generation = data.camunda_channel.alpha.default_generation_id
  region     = data.camunda_region.trial.id
  plan_type  = data.camunda_cluster_plan_type.trial.id
}

output "cluster_id" {
  value = camunda_cluster.test.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `client_id` (String) Client ID to authenticate against Camunda SaaS
- `client_secret` (String) Client Secret to authenticate against Camunda SaaS

### Optional

- `api_url` (String) URL to Camunda SaaS API
- `audience` (String) Audience of the token
- `debug` (Boolean) Enable debug logs
- `token_url` (String) URL to fetch token from
